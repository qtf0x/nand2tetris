// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM16K.hdl
/**
 * Memory of 16K 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    /* select which memory bank's load pin to send the load signal to */
    DMux4Way(in=load, sel=address[12..13], a=loadA, b=loadB, c=loadC, d=loadD);

    /* all banks wired input simultaneously, only one gets load signal */
    RAM4K(in=in, load=loadA, address=address[0..11], out=ramA);
    RAM4K(in=in, load=loadB, address=address[0..11], out=ramB);
    RAM4K(in=in, load=loadC, address=address[0..11], out=ramC);
    RAM4K(in=in, load=loadD, address=address[0..11], out=ramD);

    /* select which bank's output signal to send to output pin */
    Mux4Way16(a=ramA, b=ramB, c=ramC, d=ramD, sel=address[12..13], out=out);
}
