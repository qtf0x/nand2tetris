// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM4K.hdl
/**
 * Memory of 4K 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM4K {
    IN in[16], load, address[12];
    OUT out[16];

    PARTS:
    /* select which memory bank's load pin to send the load signal to */
    DMux8Way(in=load, sel=address[9..11], a=loadA, b=loadB, c=loadC, d=loadD,
    e=loadE, f=loadF, g=loadG, h=loadH);

    /* all banks wired input simultaneously, only one gets load signal */
    RAM512(in=in, load=loadA, address=address[0..8], out=ramA);
    RAM512(in=in, load=loadB, address=address[0..8], out=ramB);
    RAM512(in=in, load=loadC, address=address[0..8], out=ramC);
    RAM512(in=in, load=loadD, address=address[0..8], out=ramD);
    RAM512(in=in, load=loadE, address=address[0..8], out=ramE);
    RAM512(in=in, load=loadF, address=address[0..8], out=ramF);
    RAM512(in=in, load=loadG, address=address[0..8], out=ramG);
    RAM512(in=in, load=loadH, address=address[0..8], out=ramH);

    /* select which bank's output signal to send to output pin */
    Mux8Way16(a=ramA, b=ramB, c=ramC, d=ramD, e=ramE, f=ramF, g=ramG, h=ramH,
    sel=address[9..11], out=out);
}
