// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with increment, load, and reset modes.
 * if      (inc(t))   out(t+1) = out(t) + 1
 * else if (load(t))  out(t+1) = in(t)
 * else if (reset(t)) out(t+1) = 0
 * else               out(t+1) = out(t)
 *
 * To select a mode, assert the relevant control bit,
 * and de-assert the other two bits. 
 */
CHIP PC {
    IN in[16], inc, load, reset;
    OUT out[16];
    
    PARTS:
    /* option precedence lowest-highest */

    /* first select between 0 and the output of the incrementer */
    Mux16(a=false, b=incOut, sel=inc, out=incOn);

    /* then between the above output and the chip input */
    Mux16(a=incOn, b=in, sel=load, out=loadOn);

    /* finally between the above output and 0 (reset) */
    Mux16(a=loadOn, b=false, sel=reset, out=resetOn);

    /* incrementer is always going, but output might not be selected */
    Inc16(in=incIn, out=incOut);

    /* only set load pin on register if there's something to load,
       else we just want to output register state */
    Or(a=load, b=inc, out=xor1);
    Or(a=xor1, b=reset, out=pinSet);

    /* feed output to incrementer in case that gets selected AND to chip out */
    Register(in=resetOn, load=pinSet, out=incIn, out=out);
}
